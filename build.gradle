plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.140'
    id 'io.freefair.lombok' version '8.6'
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven {
        url "https://maven.blamejared.com/"
        content {
            includeGroup "mezz.jei"
        }
    }
    maven { // TOP
        url "https://maven.k-4u.nl"
        content {
            includeGroup "mcjty.theoneprobe"
        }
    }
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
        content {
            includeGroup 'appeng'
            includeGroup 'mekanism'
            includeGroup 'me'
        }
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        url "https://maven.ryanliptak.com/"
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
            jvmArgument '-Dneoforge.logging.shutdownDebug=true'
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

test {
    useJUnitPlatform()
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"

    compileOnly("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    compileOnly("mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}")
    //runtimeOnly("mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}")

    compileOnly "dev.emi:emi-neoforge:${emi_version}:api"
    runtimeOnly "dev.emi:emi-neoforge:${emi_version}"

    //runtimeOnly "mcjty.theoneprobe:theoneprobe:${top_version}"
    runtimeOnly "curse.maven:jade-324717:5109393"
    runtimeOnly "appeng:appliedenergistics2-neoforge:${ae2_version}"

    compileOnly "squeek.appleskin:appleskin-neoforge:mc${appleskin_version}:api"
    runtimeOnly "squeek.appleskin:appleskin-neoforge:mc${appleskin_version}"

    implementation "curse.maven:modern-dynamics-552758:5373764" //Modern Industrialization
    implementation "mekanism:Mekanism:${mekanism_version}"// Mekanism
    implementation "mekanism:Mekanism:${mekanism_version}:additions"// Mekanism: Additions
    implementation "mekanism:Mekanism:${mekanism_version}:generators"// Mekanism: Generators
    implementation "mekanism:Mekanism:${mekanism_version}:tools"// Mekanism: Tools

    implementation "curse.maven:pneumaticcraft-repressurized-281849:5523027"
    /*runtimeOnly "curse.maven:cloth-config-348521:5729139"
    runtimeOnly "curse.maven:powah-rearchitected-633483:5083059"*/
    runtimeOnly "curse.maven:spark-361579:5243943"
    runtimeOnly "curse.maven:energized-power-782147:5401190"

    testImplementation(platform("org.junit:junit-bom:${junit_version}"))
    testCompileOnly "org.junit.jupiter:junit-jupiter-api"
    testCompileOnly "org.junit.jupiter:junit-jupiter-params"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.mockito:mockito-core:5.8.0"
    testImplementation "org.mockito:mockito-junit-jupiter:5.8.0"
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
